/*
  # HEADER #
	Script: 		Common\Functions\Common_GetTownsResources.sqf
	Alias:			CTI_CO_FNC_GetTownsResources
	Description:	Get the sum of the resources generated by towns for a given side
	Author: 		Benny
	Creation Date:	18-09-2013
	Revision Date:	19-01-2015 (Sari)
	
  # PARAMETERS #
    0	[Side]: The side
	
  # RETURNED VALUE #
	[Array]: The current side upgrades
	
  # SYNTAX #
	(SIDE) call CTI_CO_FNC_GetTownsResources
	
  # DEPENDENCIES #
	Common Function: CTI_CO_FNC_GetSideID
	
  # EXAMPLE #
    _income = (West) call CTI_CO_FNC_GetTownsResources
*/

private ["_hostiles", "_maxSV", "_maxvalue", "_near", "_occupation_upgrade", "_side", "_sideID", "_supply", "_total_supply", "_total_values", "_unoccupied", "_upgrades", "_value"];

_side = _this;
_total_values = 0;
_total_supply = 0;

_sideID = (_side) call CTI_CO_FNC_GetSideID;
_upgrades = (_side) call CTI_CO_FNC_GetSideUpgrades;
_occupation_upgrade = _upgrades select CTI_UPGRADE_TOWNS;

{
	if (_x getVariable "cti_town_sideID" == _sideID) then {
		_maxvalue = _x getVariable "cti_town_value";
		_supply = _x getVariable "cti_town_SV";
		_maxSV = _x getVariable "cti_town_maxSV";
		_value = _maxvalue * (_supply / _maxSV);
		
		if (missionNamespace getVariable "CTI_ECONOMY_TOWNS_OCCUPATION" == 1) then { //--- Town occupation is active
			_near = _x nearEntities ["AllVehicles", CTI_MARKERS_TOWN_AREA_RANGE];
			_unoccupied = CTI_TOWNS_INCOME_UNOCCUPIED_PERCENTAGE select _occupation_upgrade;
			if (_side countSide _near > 0) then { //--- The town is occupied by friendlies, but what about enemies?
				_hostiles = 0;
				{_hostiles = _hostiles + (_x countSide _near)} forEach ([west, east, resistance] - [_side]);
				
				if (_hostiles > 0) then { //--- Hostiles in range, not that occupied heh?
					_value = _value * _unoccupied;
					_supply = _supply * _unoccupied;
				};
			} else { //--- Unoccupied
				_value = _value * _unoccupied;
				_supply = _supply * _unoccupied;
			};
		};
		
		_value = _value * CTI_TOWNS_INCOME_RATIO; //--- Add in the extra ratio
		_supply = _supply * CTI_TOWNS_INCOME_RATIO;
		_total_values = _total_values + round(_value);
		_total_supply = _total_supply + round(_supply);
	};
} forEach CTI_Towns;

[_total_values, _total_supply]